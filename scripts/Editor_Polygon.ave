' uses the built-in function View.PolyTool

' ################################
' REV: 09.01.2026, A. Boehlen
' - Check theme
' - Set timestamp for new features
' ################################

theView = av.GetActiveDoc
theTheme = theView.GetEditableTheme

if (av.Run("Editor.SubCheckTheme", {theTheme, "polygon", "false", "0"}) = nil) then
  return nil
end

theFTab = theTheme.GetFTab

'# create string field DateOfCr if it doesn't exist
if (theFTab.FindField("DateOfCr") = nil) then
  av.Run("Editor.SubCreateField", {theFTab, "DateOfCr", "16"})
end

p = theView.ReturnUserPolygon
if (p.IsNull) then
  return nil
else
  theFTab.BeginTransaction
  thePrj = theView.GetProjection
  if (thePrj.IsNull.Not) then
    p = p.ReturnUnprojected(thePrj)
  end
  theField = theTheme.GetFTab.FindField("Shape")
  theDate = theTheme.GetFTab.FindField("DateOfCr")
  rec = theTheme.GetFTab.AddRecord
  theFTab.SetValue(theField, rec, p)
  theFTab.GetSelection.ClearAll
  theFTab.GetSelection.Set(rec)
  theFTab.UpdateSelection
  
  '# set timestamp as string
  timestampStr = av.Run("Editor.SubCreateDate", {})
  theFTab.SetValue(theDate,rec,timestampStr)
  
  theFTab.EndTransaction
  av.GetProject.SetModified(true)
end

