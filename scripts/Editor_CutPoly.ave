' uses the built-in function View.SplitTool

' ##########################################
' REV: 09.01.2026, A. Boehlen
' - Set timestamp for new features
' - clear selected features and refresh view
' ##########################################

theView = av.GetActiveDoc
theTheme = theView.GetEditableTheme

if (av.Run("Editor.SubCheckTheme", {theTheme, "polygon", "true", "1"}) = nil) then
  return nil
end

theFTab = theTheme.GetFTab

l = theView.ReturnUserPolyLine
if (l.IsNull) then
  return nil
else
  theTheme.GetFtab.BeginTransaction
  theField = theTheme.GetFTab.FindField("Shape")
  theDate = theFTab.FindField("DateOfCr")
  theType = theField.GetType
  if ((theType = #FIELD_SHAPEPOLY) or (theType = #FIELD_SHAPELINE)) then
    theTheme.Split(l)
  end
  
  '# set timestamp as string for the selected features
  theBitmap = theFTab.getSelection 

  i = 0
  for each rec in theBitmap
    theVal = theFTab.ReturnValue (theField, rec)
    if ((theVal.asString) = "Polyline: Null") then
      theBitmap.set(rec)
    else
      timestampStr = av.Run("Editor.SubCreateDate", {})
      theFTab.SetValue(theDate,rec,timestampStr)

      theBitmap.clear(rec)
    end
  end
  
  theTheme.GetFtab.EndTransaction 
  av.GetProject.SetModified(true)
end

'# clear selection and refresh view
theFTab.BeginTransaction
theTheme.ClearSelected
theFTab.EndTransaction
theTheme.Invalidate (TRUE)
theView.InvalidateTOC( nil )
theView.Invalidate
System.RefreshWindows


