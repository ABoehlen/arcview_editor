' NAME: makeRectByCenter
' HERKUNFT: Harald 11 / 04 / 03,  zur Erstellung von Rastergebieten fuer Windgutachten
' EINBINDUNG: View-Tool
' HILFE: Creates Circle from Click Point as Center//Creates a Circle of user defined Radius with User Point as Center (Coordinates can be adjusted)

' ################################
' REV: 07.01.2026, A. Boehlen
' - Set timestamp for new features
' ################################


'*******************************************

' -------------------------------------------------------------------------------------------------
' SET NUMBER-FORMAT

 Script.The.SetNumberFormat( "d.dddd" )
 
' -------------------------------------------------------------------------------------------------
' TAKE THE VIEW; THE PROJECTION AND THE MAP-UNITS

theView = av.GetActiveDoc
theProjection = theView.GetProjection

ViewUnit = theView.GetUnits
MapU = units.GetUnitString(ViewUnit)

if (MapU = " ") then
  MapU = "Map Units"
end

' -------------------------------------------------------------------------------------------------
' RETURN THE POINT; THE USER CLICKS AT - GIVE HER THE CHANCE TO TAKE THE LAST DEFINED POINT 
' TO CREATE CONCENTRIC CIRCLES

theVari = "theUPX"

theTestScript = Script.Make("return _"+theVari)
if (theTestScript.HasError) then
  _theUPX = NIL
end

theVari = "theUPY"

theTestScript = Script.Make("return _"+theVari)
if (theTestScript.HasError) then
  _theUPY = NIL
end

if ((_theUPX <> NIL) and (_theUPY <> NIL)) then
  theChoice = MsgBox.YesNoCancel("Take the last defined Point  (YES)"+nl+
  "Take the new Click Point  (NO)"," Coordinate Takeover",TRUE)
else
  theChoice = FALSE
end

if (theChoice = FALSE) then
  theUP = theView.GetDisplay.ReturnUserPoint
  theUPXString = theUP.getX.AsString
  theUPYString = theUP.getY.AsString
elseif (theChoice = TRUE) then
  theUPXString = _theUPX.AsString
  theUPYString = _theUPY.AsString
else
  return NIL
end

' -------------------------------------------------------------------------------------------------
' GET THE EXTEND AND LET THE USER MODIFY THE CENTER-COORDINATE

labels = {"X Coordinate","Y Coordinate"}  
defaults = {theUPXString, theUPYString }

theExt = MsgBox.MultiInput("Point Coordinates (adjust if necessary):", " Point Coordinates", labels, defaults)

if ((theExt = NIL) or (theExt.count = 0)) then
  av.ShowMsg("llll llll llll llll llll   llll llll llll    DROPOUT: Make Point...")
  return nil
end

_theUPX = theExt.get(0).AsNumber
_theUPY = theExt.get(1).AsNumber

' -------------------------------------------------------------------------------------------------
' CREATE A GRAPHIC WITH POINT AS CENTER

thePoint = Point.Make(_theUPX,_theUPY)

thePointGR = GraphicShape.Make(thePoint)
theView.GetGraphics.UnselectAll
thePointGR.SetSelected(TRUE)
theView.GetGraphics.Add(thePointGR)

thePointSH = thePointGR.GetShape

theCentroid = thePoint.ReturnCenter
theCentX = theCentroid.GetX
theCentY = theCentroid.GetY

av.GetProject.SetModified(true)

' -------------------------------------------------------------------------------------------------
' ASK THE USER IF SHE WANTS TO CREATE A POLYGON SHAPE OR JUST KEEP THE GRAPHIC. IF FORMER, THAN ASK HER 
' IF SHE WANTS TO INSERT THE POINT IN AN AVAILABLE THEME OR IF SHE WANTS TO CREATE A NEW ONE


' FIRST CREATE A LIST OF VIABLE THEMES

thePointThms = List.Make

for each t in theView.GetThemes
  if (t.Is(Ftheme)) then
      theFTab = t.GetFTab
      theCN = theFTab.GetShapeClass.GetClassName
      if ((theCN = "Point") or (theCN = "PointZ")) then
      thePointThms.Add(t)
    end
  end
end

' - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
' NOW ASK FOR SHAPE - AVAILABLE OR NEW

if (thePointThms.Count > 0) then
  theChoice = MsgBox.YesNoCancel("Convert Point to new Theme  (YES) "+nl+
                   "Insert Point into existing Theme  (NO)"+nl+
                   "Create Point as Graphic  (CANCEL)"," Convert to Shapefile?",TRUE)
  anAvail = TRUE
else
  theChoice = MsgBox.YesNo("Convert Point to new Theme  (YES) "+nl+
                   "Create Point as Graphic  (NO)"," Convert to Shapefile?",TRUE)  
  anAvail = FALSE
end
   
' - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
' IF THERE ARE MORE THAN ONE VIABLE THEMES, ASK FURTHER IF SHE WANTS TO INSERT THE RECTANGLE THERE
  
if (anAvail = TRUE) then
  
  if (theChoice = FALSE) then    

    nuTheme = FALSE

'     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
'     FIND THE RIGHT THEME IF THERE ARE MORE THAN ONE

    if (thePointThms.Count < 1) then
      theAvail = NIL
    elseif (thePointThms.Count = 1) then
      theAvail = thePointThms.Get(0)
      theOnlyFit = MsgBox.YesNo("Insert the Point into  '"+theAvail.getName+"'?"," Take that?",TRUE)
      if (theOnlyFit = FALSE) then
        av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
        return NIL
      end

    else
      theAvail = Msgbox.List(thePointThms,"Select a Theme to insert the Point."," Select Theme")
    end
    
    if (theAvail = NIL) then
      av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
      return NIL
    end
    
    theFTab = theAvail.getFTab
    editState = theFTab.isEditable
    
'     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
'     LOOK FOR OUR FIELDS; CREATE THEM IF THEY AREN'T YET
'
'    theFldList = List.Make
'    
'    if ((theFTab.findField("IDN") = NIL)) then
'      theIDfld = Field.Make("IDN",#FIELD_LONG,9, 0)
'     theFldList.Add(theIDfld)
'    else
'      theIDfld = theFTab.findField("IDN")
'    end
'    
'    if ((theFTab.findField("X_Centr") = NIL)) then
'      theCoordXZentFld = Field.Make("X_Centr",#FIELD_DECIMAL,21,4)
'      theFldList.Add(theCoordXZentFld)
'    else
'      theCoordXZentFld = theFTab.findField("X_Centr")
'    end
'
'    if ((theFTab.findField("Y_Centr") = NIL)) then
'      theCoordYZentFld = Field.Make("Y_Centr",#FIELD_DECIMAL,21,4)
'      theFldList.Add(theCoordYZentFld)
'    else
'      theCoordYZentFld = theFTab.findField("Y_Centr")
'    end
'    
    theShapeFld=theFtab.FindField("Shape")

'   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
'    MAKE SURE TABLE IS EDITABLE AND THAT FIELDS CAN BE ADDED
    
    if (theFtab.CanEdit) then
      theFTab.SetEditable(true)
      if ((theFTab.CanAddFields).Not) then
        MsgBox.Info("Can not add Fields to the FTab. Please check Writing Permission."+nl+
        "Point will be created as a Graphic..."," Not able to edit")
        av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
        theFTab.setEditable(editstate)
        return NIL
      end
    else
      MsgBox.Info("Can not edit the Table. Please check Writing Permission.",
      "Point will be created as a Graphic..."," Not able to edit")
      av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
      return NIL
    end

'   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
'   ADD OUR FIELDS AND SET THE VALUES
'
'    theFTab.AddFields(theFldList)
'    
    theRecNo = theFtab.AddRecord
'    theFtab.SetValue(theIDfld, theRecNo, theRecNo)
    if (theProjection.IsNull) then
      thePointSH2 = thePointSH.Clone
    else
      thePointSH2 = thePointSH.ReturnUnProjected(theProjection)
    end
    myShape = thePointSH2
    theFtab.SetValue(theShapeFld,theRecNo,myShape)
'    
'    theFtab.SetValue(theCoordXZentFld, theRecNo, theCentX)
'    theFtab.SetValue(theCoordYZentFld, theRecNo, theCentY)
'    
'    theFTab.SetEditable(editState)
'    theAvail.SetVisible(TRUE)
'
    '# create string field DateOfCr if it doesn't exist
    if (theFTab.FindField("DateOfCr") = nil) then
      av.Run("Editor.SubCreateField", {theFTab, "DateOfCr", "16"})
    end

    '# set timestamp as string
    theDateFld = theFTab.FindField("DateOfCr")
    timestampStr = av.Run("Editor.SubCreateDate", {})
    theFTab.SetValue(theDateFld,theRecNo,timestampStr)


    theView.GetGraphics.CutSelected
    av.GetProject.SetModified(true)
    
    av.ShowMsg("llll llll llll llll llll   llll llll llll    Inserted the Point in  "+theAvail.GetName+"...")

  elseif (theChoice = TRUE) then  ' USER WANTS TO CREATE A NEW THEME
    nuTheme = TRUE
  else                       ' theChoice is NIL: USER DON'T WANT A SHAPEFILE BUT THE GRAPHIC
    nuTheme = FALSE
    av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
    return NIL
  end
end

' - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
' NOW WE HAVE THE CASES IN WHICH WE SHOULD CREATE A NEW SHAPE

if ((anAvail = FALSE) or (nuTheme = TRUE)) then

  if (theChoice = TRUE) then

    theCWD = FileName.GetCWD
    defname = theCWD.MakeTMP("mr_pnt", "shp")
    theOutput = FileDialog.Put(defname,"*.shp","NAME OF THE NEW THEME")
    if (theOutput = nil) then
      return NIL
    end
    
    theFtab=Ftab.MakeNew(theOutput, Point)
    theIDfld=Field.Make("IDN", #FIELD_LONG, 6, 0)
    theCoordXZentFld = Field.Make("X_Centr",#FIELD_DECIMAL,18,4)
    theCoordYZentFld = Field.Make("Y_Centr",#FIELD_DECIMAL,18,4)
    theDateFld = Field.Make("DateOfCr",#FIELD_CHAR,16,0)
    theShapeFld=theFtab.FindField("Shape")
    theFldList = {theIDfld,theCoordXZentFld,theCoordYZentFld,theDateFld}
    theFtab.AddFields(theFldList)
    
    theRecNo=theFtab.AddRecord
    theFtab.SetValue(theIDfld, theRecNo, theRecNo)
    if (theProjection.IsNull) then
      thePointSH2 = thePointSH.Clone
    else
      thePointSH2 = thePointSH.ReturnUnProjected(theProjection)
    end
    myShape=thePointSH2
    theFtab.SetValue(theShapeFld, theRecNo, myShape)
    
    theFtab.SetValue(theCoordXZentFld, theRecNo, theCentX)
    theFtab.SetValue(theCoordYZentFld, theRecNo, theCentY)
    
    '# set timestamp as string
    timestampStr = av.Run("Editor.SubCreateDate", {})
    theFTab.SetValue(theDateFld,theRecNo,timestampStr)

  
    theFTheme=Ftheme.Make(theFTab)
    theView.AddTheme(theFTheme)
    
    theFTab.SetEditable(FALSE)
    
  ' -------------------------------------------------------------------------------------------------
  ' DELETE THE GRAPHIC; SET THE THEME TRANSPARENT AND OUTLINE RED
  
    theView.GetGraphics.CutSelected
    av.GetProject.SetModified(true)
    
    theLegend = theFTheme.GetLegend
    theShapeColor = Color.GetRed
    theSymList = theLegend.GetSymbols
    theLegSym = theSymList.Get(0).getColor
'    theLegSym.SetTransparent (TRUE)
    theSym = av.GetSymbolWin.GetPalette.GetList(#PALETTE_LIST_Marker).Get(0).Clone
    theSym.SetColor(theShapeColor)
    theSym.setSize(8)
    theSymList.Set(0,theSym)
  
    theFTheme.UpdateLegend
    theFTheme.SetVisible(TRUE)
    theView.InvalidateTOC(theFTheme)
    av.ShowMsg("llll llll llll llll llll   llll llll llll    Created Point as  "+theFTheme.GetName+"...")
  else
    av.ShowMsg("llll llll llll llll llll   llll llll llll    Point created as a GRAPHIC  (X = "+_theUPX.AsString+";  Y = "+_theUPY.AsString+") ...")
  end
end

theView.Invalidate

' -------------------------------------------------------------------------------------------------
' END OF FILE

