' uses the built-in function View.LineTool

' ################################
' REV: 10.01.2026, A. Boehlen
' - Check theme
' - Set timestamp for new features
' ################################

theView = av.GetActiveDoc
theTheme = theView.GetEditableTheme

if (av.Run("Editor.SubCheckTheme", {theTheme, "polyline", "false", "0"}) = nil) then
  return nil
end

theFTab = theTheme.GetFTab

'# create string field DateOfCr if it doesn't exist
if (theFTab.FindField("DateOfCr") = nil) then
  av.Run("Editor.SubCreateStrField", {theFTab, "DateOfCr", "16"})
end

l = theView.ReturnUserPolyLine
if (l.IsNull) then
  return nil
else
  theTheme.GetFtab.BeginTransaction
  thePrj = theView.GetProjection
  if (thePrj.IsNull.Not) then
    l = l.ReturnUnprojected(thePrj)
  end
  theField = theFTab.FindField("Shape")
  theDate = theFTab.FindField("DateOfCr")
  rec = theFTab.AddRecord
  theFTab.SetValue(theField, rec, l)
  theFTab.GetSelection.ClearAll
  theFTab.GetSelection.Set(rec)
  theFTab.UpdateSelection
  theFtab.EndTransaction
  
  '# set timestamp as string
  timestampStr = av.Run("Editor.SubCreateDate", {0})
  theFTab.SetValue(theDate,rec,timestampStr)
  
  av.GetProject.SetModified(true)
end
