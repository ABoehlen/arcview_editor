' uses the built-in script View.RectTool

' ################################
' REV: 09.01.2026, A. Boehlen
' - Check theme
' - Set timestamp for new features
' ################################

theView = av.GetActiveDoc

r = theView.ReturnUserRect
theTheme = theView.GetEditableTheme

if (av.Run("Editor.SubCheckTheme", {theTheme, "polygon", "false", "0"}) = nil) then
  return nil
end

theFTab = theTheme.GetFTab

'# create string field DateOfCr if it doesn't exist
if (theFTab.FindField("DateOfCr") = nil) then
  av.Run("Editor.SubCreateField", {theFTab, "DateOfCr", "16"})
end

if (r.IsNull) then
  return nil
else
  if (theTheme <> nil) then
    p = r.AsPolygon
    thePrj = theView.GetProjection
    if (thePrj.IsNull.Not) then
      p = p.ReturnUnprojected(thePrj)
    end
    theFTab.BeginTransaction
    theField = theFTab.FindField("Shape")
    theDate = theTheme.GetFTab.FindField("DateOfCr")
    rec = theFTab.AddRecord
    theFTab.SetValue(theField, rec, p)
    theFTab.EndTransaction
    theFTab.GetSelection.ClearAll
    theFTab.GetSelection.Set(rec)
    theFTab.UpdateSelection
    
    '# set timestamp as string
    timestampStr = av.Run("Editor.SubCreateDate", {})
    theFTab.SetValue(theDate,rec,timestampStr)
    
  else
    gr = GraphicShape.Make(r)
    theView.GetGraphics.UnselectAll
    gr.SetSelected(TRUE)
    theView.GetGraphics.Add(gr)
  end
  av.GetProject.SetModified(true)
end
