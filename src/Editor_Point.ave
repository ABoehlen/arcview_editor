' uses the built-in function View.PointTool

' ################################
' REV: 10.01.2026, A. Boehlen
' - Check theme
' - Set timestamp for new features
' ################################


theView = av.GetActiveDoc
theTheme = theView.GetEditableTheme

if (av.Run("Editor.SubCheckTheme", {theTheme, "point", "false", "0"}) = nil) then
  return nil
end

theFTab = theTheme.GetFTab

'# create string field DateOfCr if it doesn't exist
if (theFTab.FindField("DateOfCr") = nil) then
  av.Run("Editor.SubCreateStrField", {theFTab, "DateOfCr", "16"})
end

pt = theView.GetDisplay.ReturnUserPoint
thePrj = theView.GetProjection
if (thePrj.IsNull.Not) then
  pt = pt.ReturnUnprojected(thePrj)
end

theField = theTheme.GetFTab.FindField("Shape")
theDate = theTheme.GetFTab.FindField("DateOfCr")
theTheme.GetFTab.BeginTransaction
rec = theTheme.GetFTab.AddRecord
theFTab.SetValue(theField, rec, pt)
theFTab.EndTransaction
theFTab.GetSelection.ClearAll
theFTab.GetSelection.Set(rec)
theFTab.UpdateSelection

'# set timestamp as string
timestampStr = av.Run("Editor.SubCreateDate", {0})
theFTab.SetValue(theDate,rec,timestampStr)

av.GetProject.SetModified(true)

